# We need first a custom target to copy all the input files in the compilation # folder
file(GLOB MooringConfigFiles ${CMAKE_SOURCE_DIR}/tests/Mooring/*.stl
                             ${CMAKE_SOURCE_DIR}/tests/Mooring/*.dat
                             ${CMAKE_SOURCE_DIR}/tests/Mooring/*.txt)
add_custom_target(Test_MooringConfigFiles)
add_custom_command(TARGET Test_MooringConfigFiles PRE_BUILD
                   COMMAND ${CMAKE_COMMAND} -E
                        copy_directory ${CMAKE_SOURCE_DIR}/tests/Mooring ${CMAKE_CURRENT_BINARY_DIR}/Mooring
)
if(WIN32)
    add_custom_command(TARGET Test_MooringConfigFiles POST_BUILD
                       COMMAND ${CMAKE_COMMAND} -E copy $<TARGET_FILE:moordyn> ${CMAKE_CURRENT_BINARY_DIR}/
    )
    if(${FORTRAN_WRAPPER})
        add_custom_command(TARGET Test_MooringConfigFiles POST_BUILD
                           COMMAND ${CMAKE_COMMAND} -E copy $<TARGET_FILE:moordynf> ${CMAKE_CURRENT_BINARY_DIR}/
        )
    endif()
endif()

set(C_TESTS 
    c_api
)

set(CPP_TESTS 
    minimal
    lifting
    pendulum
    beam
    time_schemes
    io
    rods
    bodies_and_rods
    quasi_static_chain
    wavekin_4
    tripod
    seafloor
    wavekin
    wavekin_7
    bodies
)

set(CATCH2_TESTS 
    math_tests
    testDecomposeString
    polyester
)

function(make_executable test_name, extension)
    set(TEST_PRIVATE_DEPS "moordyn;")
    add_executable(${test_name} ${test_name}.${extension})
    if(USE_VTK AND MOORDYN_PACKAGE_IGNORE_VTK_DEPENDENCY)
        list(APPEND TEST_PRIVATE_DEPS VTK::CommonCore
                                         VTK::IOCore
                                         VTK::IOXML
                                         VTK::IOGeometry
                                         VTK::FiltersGeneral)
        get_target_property(TEST_PRIVATE_INCLUDE_DIRS
                            VTK::CommonCore
                            INTERFACE_INCLUDE_DIRECTORIES)
        target_include_directories(${test_name}
                                   PRIVATE ${TEST_PRIVATE_INCLUDE_DIRS})
    endif()
    target_link_libraries(${test_name} PRIVATE ${TEST_PRIVATE_DEPS})
    add_dependencies(${test_name} Test_MooringConfigFiles)
endfunction()

function(make_c_executable test_name)
    make_executable(${test_name}, "c")
endfunction()

function(make_cpp_executable test_name)
    make_executable(${test_name}, "cpp")
    target_include_directories(${test_name} PUBLIC ${CMAKE_BINARY_DIR}/)
endfunction()

function(make_catch2_executable test_name)
    make_cpp_executable(${test_name})
    target_link_libraries(${test_name} PRIVATE Catch2::Catch2WithMain)
endfunction()

if(USE_VTK)
    set(CPP_TESTS "${CPP_TESTS};vtk")

    make_cpp_executable(wave_surface)
endif()

foreach(TEST ${C_TESTS})
    make_c_executable(${TEST})
    add_test(NAME ${TEST}
             COMMAND ${TEST})
endforeach()

foreach(TEST ${CPP_TESTS})
    make_cpp_executable(${TEST})
    add_test(NAME ${TEST}
             COMMAND ${TEST})
endforeach()

foreach(TEST ${CATCH2_TESTS})
    make_catch2_executable(${TEST})
    add_test(NAME ${TEST}
             COMMAND ${TEST})
endforeach()

if(${FORTRAN_WRAPPER})
    add_executable(fortran_bindings fortran_bindings.f90)
    target_link_libraries(fortran_bindings moordynf)
    target_include_directories(fortran_bindings PUBLIC $<TARGET_PROPERTY:moordynf,Fortran_MODULE_DIRECTORY>)
    add_dependencies(fortran_bindings Test_MooringConfigFiles)
    add_test(NAME fortran_bindings
             COMMAND fortran_bindings)
endif()

if(${PYTHON_WRAPPER})
    add_test (NAME python-tests
        COMMAND ${Python_EXECUTABLE} -m pytest
        WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
    )
    set_tests_properties(python-tests
        PROPERTIES ENVIRONMENT "PYTHONPATH=${CMAKE_BINARY_DIR}/wrappers/python/:$ENV{PYTHONPATH}")
    set_tests_properties(python-tests
        PROPERTIES ENVIRONMENT "LD_LIBRARY_PATH=${CMAKE_BINARY_DIR}/source/:$ENV{LD_LIBRARY_PATH}")
endif()

if(${MATLAB_WRAPPER})
    configure_file("${CMAKE_CURRENT_SOURCE_DIR}/matlab_bindings.m.in"
                   "${CMAKE_CURRENT_BINARY_DIR}/matlab_bindings.m")

    matlab_add_unit_test(NAME matlab_bindings
                         UNITTEST_FILE ${CMAKE_CURRENT_BINARY_DIR}/matlab_bindings.m
                         WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR})
endif()
